{
  config,
  lib,
  ...
}:
with lib; let
  cfg = config.programs.kanata;

  toKanataPairs = attrs:
    mapAttrsToList (k: v: "${k} ${toString v}") attrs
    |> concatStringsSep "\n  ";

  generateLayer = name: mapping: ''
    (deflayermap (${name})
        ${toKanataPairs mapping}
    )
  '';
in {
  config = mkIf cfg.enable {
    assertions = [
      {
        assertion = cfg.devices != [];
        message = "Kanata: You must specify at least one device in 'programs.kanata.devices'.";
      }
    ];

    # Setup the system requirements for Kanata
    environment.systemPackages = mkIf cfg.addBinaryToPath [cfg.package];
    boot.kernelModules = ["uinput"];
    hardware.uinput.enable = true;
    services.udev.extraRules = ''
      KERNEL=="uinput", MODE="0660", GROUP="uinput", OPTIONS+="static_node=uinput"
    '';

    # Configure its service
    services.kanata = {
      enable = true;
      package = cfg.package;
      keyboards.internalKeyboard = {
        devices = cfg.devices;
        extraDefCfg = "process-unmapped-keys yes";
        config = ''
            ;; Generated by Kanata NixOS module

            (deflocalkeys-linux
          ${toKanataPairs cfg.localKeys}
            )

            (defsrc
          ${unique cfg.sourceKeys |> concatStringsSep " "}
            )

            (defvar
          ${toKanataPairs cfg.variables}
            )

            (defalias
          ${toKanataPairs cfg.aliases}
            )

            ;; Primary Layer
          ${generateLayer cfg.primaryLayer cfg.layers.${cfg.primaryLayer}}

            ;; Other Layers
          ${
            removeAttrs cfg.layers [cfg.primaryLayer]
            |> mapAttrsToList generateLayer
            |> concatStringsSep "\n"
          }
        '';
      };
    };
  };
}
